' Gambas class file

Public WorkingDirectory As String
Public Files As New SubversionFile[]

Public Sub _new(WorkingDirectory As String)

  Me.WorkingDirectory = WorkingDirectory

End

Public Sub LoadFromSubversion()

  Me.Files.Clear
  Me.Load_Subversion_Information()
  Me.Add_Any_Unversioned_Missing_Files()
  Me.Add_FileName_RelativePath_IsFolder_ModifiedDate_And_Size()

End

Public Sub Load_Subversion_Information()

  Dim File As SubversionFile

  Dim xml As String
  Dim xDoc As New XmlDocument

  Dim Entry As XmlNode
  Dim Status As XmlNode
  Dim Commit As XmlNode
  Dim Value As XmlNode

  Shell "svn status --verbose --xml \"" & Me.WorkingDirectory & "\"" Wait To xml
  xDoc.FromString(xml)

  For Each Entry In xDoc.GetElementsByTagName("entry")

    File = New SubversionFile
    File.FullPath = Entry.Attributes["path"]

    For Each Status In Entry.ChildNodes
      If Status.name = "wc-status" Then

        File.Status = Status.Attributes["item"]
        File.Props = Status.Attributes["props"]

        For Each Commit In Status.ChildNodes
          If Commit.name = "commit" Then

            File.Revision = CInt(Commit.Attributes["revision"])

            For Each Value In Commit.ChildNodes

              If Value.name = "author" Then
                File.Author = Value.Value

              Else If Value.name = "date" Then
                Try File.CommitDate = CDate(Val(Value.Value))

              Endif

            Next
          Endif
        Next
      End If
    Next

    Me.Files.Add(File)
  Next

End

Public Sub Add_Any_Unversioned_Missing_Files()

  Dim FileName As String
  Dim FullPath As String
  Dim FileFound As Boolean = False
  Dim File As SubversionFile

  For Each FileName In RDir(Me.WorkingDirectory)
    FullPath = Me.WorkingDirectory &/ FileName
    FileFound = False

    For Each File In Me.Files
      If Lower(File.FullPath) = Lower(FullPath) Then
        FileFound = True
        Break
      Endif
    Next

    If Not FileFound Then
      File = New SubversionFile
      File.FullPath = FullPath
      Me.Files.Add(file)
    Endif

  Next

End

Public Sub Add_FileName_RelativePath_IsFolder_ModifiedDate_And_Size()

  Dim File As SubversionFile

  For Each File In Me.Files
    File.FileName = Right(File.FullPath, - RInStr(File.FullPath, "/"))
    File.RelativePath = Right(File.FullPath, - Len(Me.WorkingDirectory))
    File.IsFolder = IsDir(File.FullPath)
    If Not File.IsFolder Then
      With Stat(File.FullPath)
        File.ModifiedDate = .LastModified
        File.Size = .Size
      End With
    Endif
  Next

End



Public Sub Update()

  Shell "svn update \"" & Me.WorkingDirectory & "\"" Wait

End

Public Sub Commit(Message As String)

  Shell "svn commit \"" & Me.WorkingDirectory & "\" --message \"" & Replace(message, "\"", "'") & "\"" Wait

End

