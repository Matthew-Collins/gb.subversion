' Gambas class file

Public Root As SubversionRoot

Public Sub _new()

  With List

    .Columns.Count = 4

    With List.Columns[1]
      .Text = ("Status")
      .Alignment = Align.Center
      .Width = 120
    End With

    With List.Columns[2]
      .Text = ("Size")
      .Alignment = Align.Right
      .Width = 80
    End With

    With List.Columns[3]
      .Text = ("Modified")
      .Alignment = Align.Center
      .Width = 150
    End With

  End With

End

Public Sub Form_Open()

  Try WorkingDirectory.text = Args[1]

End

Public Sub PickWorkingDirectory_Click()

  Try Dialog.Path = WorkingDirectory.text
  If Not Dialog.SelectDirectory() Then
    WorkingDirectory.text = Dialog.Path
  Endif

End

Public Sub WorkingDirectory_Change()

  Me.LoadFromSubversionAndDisplay()

End

Public Sub LoadFromSubversionAndDisplay()

  Dim File As SubversionFile

  Me.Root = New SubversionRoot(WorkingDirectory.Text)
  Me.Root.LoadFromSubversion()

  List.Clear
  Tree.Clear
  Revision.text = ""

  If Me.Root.Files.Count > 0 Then

    With Me.Root.Files[0]
      Try Revision.Text = Subst(("Revision: &1 - Author: &2 - Date: &3"), .Revision, .Author, Format(.CommitDate, ("d mmmm yyyy hh:nn:ss")))
    End With

    For Each File In Me.Root.Files
      With File

        If Not .IsSquiggle() Then
          Tree.Add(.FullPath, .FileName, .GetPicture(), .GetParent()).Expanded = True
        End If

        If .IsCommitable() Then
          .IsChecked = True
          List.Add(.FullPath, .FullPath, .GetCheckedPicture())
          List[.FullPath][1] = File.StatusDisplay()
          List[.FullPath][2] = Format(File.Size, "#,##0")
          List[.FullPath][3] = Format(File.ModifiedDate, ("dd/mm/yyyy hh:nn:ss"))
        Endif

      End With
    Next

  Endif

End

Public Sub Commit_Click()

  If Not IsNull(Me.Root) Then
    Me.Root.Commit(CommitMessage.Text, True)
    Me.LoadFromSubversionAndDisplay()
    Clipboard.Copy(CommitMessage.text, Clipboard.Text)
    CommitMessage.Clear
    Message.Info(("Committed!"))
  Endif

End

Public Sub Update_Click()

  If Not IsNull(Me.Root) Then
    Me.Root.Update()
    Me.LoadFromSubversionAndDisplay()
    Message.Info(("Updated!"))
  Endif

End

Public Sub Refresh_Click()

  Me.LoadFromSubversionAndDisplay()
  Message.Info(("Refreshed!"))

End

Public Sub List_DblClick()

  Dim Item As SubversionFile

  Try Item = Me.Root.GetFile(List.Current.Key)
  If Not IsNull(Item) Then
    Item.IsChecked = Not Item.IsChecked
    List.Current.Picture = Item.GetCheckedPicture()
  Endif

End
