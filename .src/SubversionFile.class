' Gambas class file

Public FullPath As String
Public RelativePath As String
Public FileName As String
Public IsFolder As Boolean
Public Props As String
Public Status As String
Public Revision As Integer
Public Author As String
Public CommitDate As Date
Public ModifiedDate As Date
Public Size As Long
Public IsChecked As Boolean

Public Function GetParent() As String

  If Me.RelativePath = Null Then
    Return Null
  Else
    Return Left(Me.FullPath, - Len(Me.FileName) - 1)
  Endif

End

Public Function IsSquiggle() As Boolean

  Return Right(Me.FullPath, 1) = "~"

End Function

Public Function IsIgnored() As Boolean

  If Right(Me.FullPath, 1) = "~" Then
    Return True
  Else If Lower(Right(Me.FullPath, 5)) = ".lock" Then
    Return True
  Else
    Return False
  Endif

End

Public Function IsUnversioned() As Boolean

  Return Me.Status = "unversioned"

End

Public Function IsNormal() As Boolean

  Return Me.Status = "normal"

End

Public Function IsModified() As Boolean

  Return Me.Status = "modified"

End

Public Function IsAdded() As Boolean

  Return Me.Status = "added"

End

Public Sub MarkAdded()

  Me.Status = "added"

End

Public Function IsCommitable() As Boolean

  Return Not Me.IsNormal() And Not Me.IsIgnored() And Not Me.IsFolder

End

Public Function VersionControlled() As Boolean

  Return Not Me.IsUnversioned()

End

Public Function StatusDisplay() As String

  Return Upper(Left(Me.Status, 1)) & Right(Me.Status, -1)

End


Public Function GetPicture() As Picture

  If Me.IsFolder Then
    Return Picture["icon:/16/directory"]

  Else If Me.IsIgnored() Then
    Return Picture.Load("icons/ignore.png")

  Else If Me.IsModified() Then
    Return Picture["icon:/16/edit"]

  Else If Me.IsNormal() Then
    Return Picture["icon:/16/ok"]

  Else If Me.IsUnversioned() Then
    Return Picture["icon:/16/new"]

  Else If Me.IsAdded() Then
    Return Picture["icon:/16/new"]

  Else
    Return Picture["icon:/16/file"]

  Endif

End

Public Function GetCheckedPicture() As Picture

  Dim Image As New Image(36, 16, Color.Transparent, 0)

  Dim Icon As Image = Me.GetPicture().Image

  Image = Image.DrawImage(Icon, 20, 0, 16, 16, 0, 0, 16, 16)

  If Me.IsChecked Then
    Icon = Image.Load("icons/checked.png")
    Image = Image.DrawImage(Icon, 0, 0, 16, 16, 0, 0, 16, 16)
  Endif

  Return Image.Picture

End

