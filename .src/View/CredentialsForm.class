' Gambas class file

Public IsCancelled As Boolean = True
Public SiteUrl As String
Public Store As New Credentials

Const NewValue As String = "<New>"

Public Sub _new(ToSiteUrl As String)

  Dim SelectedCredentialId As Integer
  Dim SelectedCredentialIndex As Integer = -1
  Dim Item As Credential
  Dim Index As Integer

  Me.Text = Application.Title & " - Credentials"
  Me.SiteUrl = ToSiteUrl
  Try SelectedCredentialId = CInt(Settings[SiteUrl & "/CredentialId", "0"])

  'Load the Combo Box
  For Index = 0 To Store.List.Max
    Item = Store.List[Index]
    ComboBoxCredentialsId.Add(Item.Id & " - " & Item.UserName)
    If SelectedCredentialId = Item.Id Then
      SelectedCredentialIndex = Index
    Endif
  Next

  ComboBoxCredentialsId.Add(NewValue)

  Try ComboBoxCredentialsId.Index = SelectedCredentialIndex
  If ComboBoxCredentialsId.Index = -1 Then
    Try ComboBoxCredentialsId.Index = Store.List.Count
  Endif

  ComboBoxCredentialsId_Change

End

Public Sub ShowIfRequired()

  If UserName.text And If Password.text Then
    Me.IsCancelled = False
  Else
    Me.ShowDialog
  Endif

End

Public Sub ShowPassword_Click()

  Password.Password = Not ShowPassword.Value

End

Public Sub ButtonOk_Click()

  Dim Id As Integer = Store.Add(UserName.Text, Password.text).Id

  Settings[SiteUrl & "/CredentialId"] = Format(Id, "00")

  Me.IsCancelled = False
  Me.Close

End

Public Sub ButtonCancel_Click()

  Me.Close

End

Public Sub ComboBoxCredentialsId_Change()

  If ComboBoxCredentialsId.Index > -1 And If ComboBoxCredentialsId.Index <= Store.List.Max Then
    With Store.List[ComboBoxCredentialsId.Index]
      UserName.Text = .UserName
      Password.Text = .Password
    End With
  Endif

End
