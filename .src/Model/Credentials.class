' Gambas class file

Public List As New Credential[]

Public Sub _new()

  Dim CredentialId As Integer
  Dim SettingName As String

  For CredentialId = 1 To 99

    SettingName = "CredentialId." & Format(CredentialId, "00")
    If Not IsNull(Settings[SettingName & "/UserName", Null]) Then

      Dim Credential As New Credential
      Credential.Id = CredentialId
      Credential.UserName = Settings[SettingName & "/UserName"]
      Credential.Password = Settings[SettingName & "/Password"]
      List.Add(Credential)

    End If

  Next

End

Public Function Add(UserName As String, Password As String) As Credential

  Dim Item As Credential

  For Each Item In List
    If Item.UserName = UserName And If Item.Password = Password Then
      Return Item
    Endif
  Next

  Dim Credential As New Credential
  Credential.Id = GetHighestId() + 1
  Credential.UserName = UserName
  Credential.Password = Password
  List.Add(Credential)

  Save()

  Return Credential

End

Public Function Get(Id As Integer) As Credential

  Dim Item As Credential

  For Each Item In List
    If Item.Id = Id Then
      Return Item
    Endif
  Next

End

Public Function GetHighestId() As Integer

  Dim Item As Credential
  Dim Highest As Integer = 0

  For Each Item In List
    Highest = Max(Highest, Item.Id)
  Next

  Return Highest

End

Public Function Save() As Boolean

  Dim Item As Credential
  Dim SettingName As String

  For Each Item In List

    SettingName = "CredentialId." & Format(Item.Id, "00")
    Settings[SettingName & "/UserName"] = Item.UserName
    Settings[SettingName & "/Password"] = Item.Password

  Next

  Return True

End
