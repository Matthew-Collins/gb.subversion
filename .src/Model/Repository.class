' Gambas class file

Public Url As String
Public Path As String
Public Revision As Integer = -1
Public WorkingDirectory As WorkingDirectory

Public Sub _new(SiteUrl As String, Optional LocalPath As String)

  Me.Url = SiteUrl
  Me.Path = LocalPath

End

Public Sub DeleteRepository()

  Directory.DeleteDirectory(Me.Path)

End

Public Sub CreateLocalRepository()

  Directory.CreateDirectory(Directory.GetDirectoryName(Me.Path))
  Global.Run.Execute(["svnadmin", "create", Me.Path])

End

Public Sub CheckOut(CheckOutToWorkingDirectory As WorkingDirectory)

  Me.WorkingDirectory = CheckOutToWorkingDirectory
  Global.Run.Execute(["svn", "checkout", Me.Url, Me.WorkingDirectory.FullPath])

End

Public Sub Load_Subversion_Repository_Latest_Status(UserName As String, Password As String)

  Dim Xml As String
  Dim xDoc As New XmlDocument
  Dim Against As XmlNode

  Global.Run.Execute(["svn", "status", "--show-updates", "--xml", Me.WorkingDirectory.FullPath, "--no-auth-cache", "--trust-server-cert", "--non-interactive", "--username", UserName, "--password", Password], Null, True, True, ByRef Xml)

  xDoc.FromString(Xml)

  For Each Against In xDoc.GetElementsByTagName("against")

    Me.Revision = Against.Attributes["revision"]

  Next

End
